(ns browseruse.cloud.http-test
  (:require [browseruse.cloud.http :as http]
            [cheshire.core :as json]
            [clojure.test :refer [deftest is]])
  (:import [com.sun.net.httpserver HttpExchange HttpHandler HttpServer]
           [java.net InetSocketAddress]))

(defn- respond! [^HttpExchange exchange status body]
  (let [bytes (.getBytes body "UTF-8")]
    (.sendResponseHeaders exchange status (alength bytes))
    (with-open [out (.getResponseBody exchange)] (.write out bytes))))

(deftest java-http-json-transport
  (let [seen (atom nil)
        server (HttpServer/create (InetSocketAddress. "127.0.0.1" 0) 0)]
    (.createContext server "/api/v3/sessions"
                    (reify HttpHandler
                      (handle [_ exchange]
                        (reset! seen {:method (.getRequestMethod exchange)
                                      :query (some-> exchange .getRequestURI .getRawQuery)
                                      :key (some-> exchange .getRequestHeaders
                                                   (.getFirst "X-Browser-Use-API-Key"))
                                      :body (json/parse-stream
                                             (java.io.InputStreamReader.
                                              (.getRequestBody exchange)) true)})
                        (respond! exchange 201 "{\"id\":\"session-1\"}"))))
    (.start server)
    (try
      (let [port (.getPort (.getAddress server))
            request! (http/transport {:base-url (str "http://127.0.0.1:" port "/api/v3")})
            response (request! {:method :post :path "/sessions"
                                :query-params {:page_size 2 :skip nil}
                                :headers {"X-Browser-Use-API-Key" "secret"}
                                :body {:task "title"}
                                :timeout-ms 2000})]
        (is (= {:status 201 :body {:id "session-1"}} response))
        (is (= {:method "POST" :query "page_size=2" :key "secret"
                :body {:task "title"}}
               @seen)))
      (finally (.stop server 0)))))

(deftest non-json-response-is-preserved
  (let [server (HttpServer/create (InetSocketAddress. "127.0.0.1" 0) 0)]
    (.createContext server "/x"
                    (reify HttpHandler
                      (handle [_ exchange] (respond! exchange 502 "upstream unavailable"))))
    (.start server)
    (try
      (let [request! (http/transport
                      {:base-url (str "http://127.0.0.1:" (.getPort (.getAddress server)))})]
        (is (= {:status 502 :body "upstream unavailable"}
               (request! {:method :get :path "/x" :timeout-ms 10000}))))
      (finally (.stop server 0)))))
