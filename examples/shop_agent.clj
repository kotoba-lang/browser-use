(ns shop-agent
  "browser-use agent example. Runs offline with the mock model + mock browser:

     clojure -Sdeps '{:paths [\"src\" \"examples\"] :deps {io.github.com-junkawasaki/langgraph-clj {:git/tag \"v0.2.0\" :git/sha \"133740f\"}}}' \\
             -M -e \"(require 'shop-agent) (shop-agent/-main)\"

  For a real browser, implement browseruse.browser/IBrowser over your
  host (Playwright/CDP on the JVM, the page itself on a WASM host) and
  swap the real Anthropic model in (see langchain-clj)."
  (:require [browseruse.browser :as b]
            [browseruse.agent :as agent]
            [langchain.model :as model]
            [langchain.message :as msg]
            [langchain.db :as db]))

(def site
  {"https://shop.example"
   {:title "Example Shop"
    :elements [{:tag "a" :text "Pricing" :nav "https://shop.example/pricing"}
               {:tag "a" :text "Docs" :nav "https://shop.example/docs"}]}
   "https://shop.example/pricing"
   {:title "Pricing — $29/mo"
    :elements [{:tag "a" :text "Home" :nav "https://shop.example"}]}})

(defn -main [& _]
  (let [browser (b/mock-browser site "https://shop.example")
        scripted (model/mock-model
                  [(msg/ai "" {:tool-calls [{:id "1" :name "click_element"
                                             :input {:index 0}}]})
                   (msg/ai "" {:tool-calls [{:id "2" :name "done"
                                             :input {:text "Pricing is $29/mo"
                                                     :success true}}]})])
        conn (db/create-conn agent/log-schema)
        {:keys [result steps]} (agent/run {:model scripted
                                           :browser browser
                                           :task "Find the price"
                                           :history-conn conn
                                           :session-id "demo"})]
    (println "result:" result "| graph steps:" steps)
    (println "visited urls (datoms):"
             (db/q '[:find [?u ...]
                     :where [_ :action/url ?u]]
                   (db/db conn)))))
